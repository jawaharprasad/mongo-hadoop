import org.apache.tools.ant.filters.ReplaceTokens

task generateScripts() << {
    def python
    if(System.env.WORKSPACE == null) {
        python = '/usr/bin/python'
    } else { 
        python = '/mnt/jenkins/languages/python/r2.7/bin/python'
    }
    copy {
        from 'bin/templates/hadoop-template.py'
        filter ReplaceTokens, tokens: [
                PROJECT_HOME   : project.rootDir.getAbsolutePath(),

                HADOOP_HOME    : "${hadoopBinaries}/hadoop-${hadoopVersion}".toString(),
                HIVE_HOME      : "${hadoopBinaries}/hive-${hiveVersion}".toString(),
                PIG_HOME       : "${hadoopBinaries}/pig-${pigVersion}".toString(),


                HADOOP_VERSION : hadoopVersion.toString(),
                HIVE_VERSION   : hiveVersion.toString(),

                HADOOP_BINARIES: hadoopBinaries.toString(),
                BIN            : hadoopVersion.startsWith("1.") ? 'hadoop' : 'hdfs',
                PYTHON_BIN     : python
        ]
        into "build/"
        rename {
            it.replace("-template", '')
        }
    }
}

task installHadoop() << {
    new File(hadoopHome).getParentFile().mkdirs()

    def url = "http://archive.apache.org/dist/hadoop/common/hadoop-${hadoopVersion}/hadoop-${hadoopVersion}.tar.gz"

    download(url, hadoopBinaries, hadoopHome)
}

def download(url, destination, target) {
    def file = new URL(url).getPath()
    file = "${destination}/${file.substring(file.lastIndexOf('/') + 1)}"
    def count = 0;
    while (!(new File(target + "/bin").exists())) {
        try {
            if(!new File(file).exists()) {
                println "${target} not found.  Downloading from ${url} to ${file}"
            }
            download {
                src url
                dest destination
                onlyIfNewer true
            }

            if(!new File(destination).exists()) {
                println "Extracting ${file}"
            }
            copy {
                from(tarTree(resources.gzip(file)))
                into destination
            }
        } catch (Exception e) {
            println "Extraction failed: " + e.getMessage()
            println "Trying again"
            new File(target).deleteDir()
            new File(file).delete()
        }
        if(count++ > 3) {
            throw new GradleException("Failed to download after 3 attempts: ${url}");
        }
    }
}

task installHive() << {
    def url = "https://archive.apache.org/dist/hive/hive-${hiveVersion}/hive-${hiveVersion}.tar.gz"

    download(url, hadoopBinaries, hiveHome)

}

task installPig() << {
    def url = "https://archive.apache.org/dist/pig/pig-${pigVersion}/pig-${pigVersion}.tar.gz"

    download(url, hadoopBinaries, pigHome)
}

task copyFiles(dependsOn: [installHadoop, installHive, installPig]) << {
    def hadoopEtc = "${hadoopHome}/etc/hadoop"
    def hadoopLib = "${hadoopHome}/share/hadoop/common"

    println "Updating mongo jars"
    
    safeCopy("core/build/libs/mongo-hadoop-core-${project(':core').version}.jar", hadoopLib, "mongo-hadoop-core.jar")
    safeCopy("streaming/build/libs/mongo-hadoop-streaming-${project(':core').version}.jar", hadoopLib, "mongo-hadoop-streaming.jar")
    safeCopy("hive/build/libs/mongo-hadoop-hive-${project(':core').version}.jar", hiveHome + '/lib', "mongo-hadoop-hive.jar")
    safeCopy(findJar(":core", "mongo-java-driver"), hadoopLib, "mongo-java-driver.jar")
    
    println "Updating cluster configuration"
    copy {
        from 'clusterConfigs'
        into hadoopEtc
    }
}

def findJar(String proj, String filter) {
    project(proj).configurations.compile.find { it.name.startsWith(filter) }
}

task configureCluster(dependsOn: copyFiles) << {
    println "Configuring ${hadoopVersion} hadoop cluster"
    def env = [:]
    env['HADOOP_HOME']="${hadoopBinaries}/hadoop-${hadoopVersion}".toString()
    exec {
        environment << env
        commandLine 'build/hadoop.py'
        args 'start', '-format'
    }
}

task shutdownCluster << {
    println "Shutting down hadoop cluster"
    exec {
        commandLine 'build/hadoop.py'
        args 'shutdown'
    }
}

def safeCopy(fromPath, toPath, newName) {
    def copied = copy {
        from fromPath
        into toPath
        rename { newName }
    }.didWork

    if (!copied) {
        throw new GradleException("Failed to copy a file: " + fromPath, new FileNotFoundException(fromPath))
    }
}